allprojects {
    repositories {
        // Using Aliyun's public mirror which includes several repositories, including JCenter.
        // This is more robust than using the JCenter-specific mirror.
        // maven { url 'https://maven.aliyun.com/repository/public' }
        // maven { url 'https://maven.aliyun.com/repository/google' }
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    afterEvaluate { project ->
        if (project.extensions.findByName("android") != null) {
            Integer pluginCompileSdk = project.android.compileSdk
            if (pluginCompileSdk != null && pluginCompileSdk < 31) {
                project.logger.error(
                        "Warning: Overriding compileSdk version in Flutter plugin: "
                                + project.name
                                + " from "
                                + pluginCompileSdk
                                + " to 31 (to work around https://issuetracker.google.com/issues/199180389)."
                                + "\nIf there is not a new version of " + project.name + ", consider filing an issue against "
                                + project.name
                                + " to increase their compileSdk to the latest (otherwise try updating to the latest version)."
                )
                project.android {
                    compileSdk 31
                }
            }

            // Force all plugins to use official NDK r28b version
            String pluginNdkVersion = project.android.ndkVersion
            if (pluginNdkVersion != null && pluginNdkVersion != "28.2.13676358") {
                project.logger.warn(
                        "Warning: Overriding ndkVersion in Flutter plugin: "
                                + project.name
                                + " from "
                                + pluginNdkVersion
                                + " to 28.2.13676358 (official NDK r28b)."
                )
                project.android {
                    ndkVersion "28.2.13676358"
                }
            }

            // Workaround: flutter_zxing 2.2.1 bundles zxing-cpp which uses
            // std::basic_string_view<unsigned char>, a non-standard usage that
            // fails with the libc++ shipped in NDK r28+. Compiling with C++20
            // resolves the issue via char8_t support.
            // See: https://github.com/zxing-cpp/zxing-cpp/issues/822
            // TODO: Remove once flutter_zxing integrates zxing-cpp PR #836.
            if (project.name == "flutter_zxing") {
                project.android {
                    defaultConfig {
                        externalNativeBuild {
                            cmake {
                                arguments "-DCMAKE_CXX_STANDARD=20"
                            }
                        }
                    }
                }
            }

            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }

            project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                kotlinOptions {
                    jvmTarget = "17"
                }
            }
        }
    }

    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
