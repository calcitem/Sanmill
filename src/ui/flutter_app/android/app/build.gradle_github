plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    if (System.getenv('KEY_STORE_PASSWORD')) {
        keystoreProperties.setProperty('storePassword', System.getenv('KEY_STORE_PASSWORD'));
        keystoreProperties.setProperty('keyPassword', System.getenv('KEY_PASSWORD'));
        keystoreProperties.setProperty('keyAlias', System.getenv('ALIAS'));
        keystoreProperties.setProperty('storeFile', System.getenv('KEY_PATH'));
        // Optional override. Defaults to type inferred from file extension.
        if (System.getenv('KEY_STORE_TYPE')) {
            keystoreProperties.setProperty('storeType', System.getenv('KEY_STORE_TYPE'));
        }
	} else {
	    throw new GradleException("key.properties not found. Copy key.properties.example to key.properties and modify it to suit your needs.")
	}
}

// JDK 9+ defaults keystore.type to PKCS12. If a JKS file is provided (common for
// Android signing), not setting storeType can cause parsing errors like:
// "Tag number over 30 is not supported". Infer a sensible default from the
// keystore file extension, but allow explicit override via key.properties/env.
def inferKeyStoreType = { String storeFilePath ->
    if (storeFilePath == null) {
        return null
    }
    def lower = storeFilePath.toLowerCase()
    if (lower.endsWith('.jks') || lower.endsWith('.keystore')) {
        return 'jks'
    }
    if (lower.endsWith('.p12') || lower.endsWith('.pfx')) {
        return 'pkcs12'
    }
    return null
}

android {
    ndkVersion "29.0.14206865"

    compileSdkVersion 36
    namespace "com.calcitem.sanmill"

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "com.calcitem.sanmill"
        minSdkVersion 19
        targetSdkVersion 36
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        ndk{
            ldLibs "log"
            debugSymbolLevel 'FULL'
        }
        externalNativeBuild {
            cmake {
                //arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared"
                //abiFilters "arm64-v8a"
            }
        }
    }

    signingConfigs {
        release {
            def storeFilePath = (keystoreProperties['storeFile'] ?: '').toString().trim()
            if (storeFilePath.isEmpty()) {
                throw new GradleException("Signing keystore path is missing. Set storeFile in key.properties or KEY_PATH env var.")
            }

            def resolvedStoreFile = file(storeFilePath)
            if (!resolvedStoreFile.exists()) {
                throw new GradleException("Signing keystore file not found at: ${resolvedStoreFile}")
            }

            def storeTypeValue = (keystoreProperties['storeType'] ?: inferKeyStoreType(storeFilePath))?.toString()

            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile resolvedStoreFile
            storePassword keystoreProperties['storePassword']
            if (storeTypeValue != null && !storeTypeValue.trim().isEmpty()) {
                storeType storeTypeValue
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        release {
            signingConfig signingConfigs.release
            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

flutter {
    source '../..'
}

dependencies {
}
